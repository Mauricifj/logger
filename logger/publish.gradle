apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'
group 'com.mauricifj'
description 'Logger Library is a publication test'
def package_name = 'logger'
def repository = 'logger'

// Create the pom configuration:
def pomConfig = {
    developers {
        developer {
            id "mauricifj"
            name "Maurici Ferreira Junior"
            email "m.f.j@hotmail.com"
        }
    }

    scm {
        url "https://github.com/Mauricifj"
    }
}

publishing {
    publications {
        production(MavenPublication) {
            groupId this.group
            artifactId package_name
            version this.version

            // Define this explicitly if using implementation or api configurations
            pom.withXml {
                def root = asNode()
                root.appendNode('description', this.description)
                root.appendNode('name', 'Logger Library')
                root.children().last() + pomConfig

                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['production']
    publish = true
    override = true
    pkg {
        repo = repository
        name = package_name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Mauricifj/logger.git'
        publicDownloadNumbers = true
        version {
            name = this.version
            desc = this.description
            released  = new Date()
        }
    }
}