apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// Create the pom configuration:
def pomConfig = {
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }

    scm {
        url developerUrl
    }
}

publishing {
    publications {
        production(MavenPublication) {
            groupId publishedGroupId
            artifactId artifact
            version libraryVersion

            // Define this explicitly if using implementation or api configurations
            pom.withXml {
                def root = asNode()
                root.appendNode('description', libraryDescription)
                root.appendNode('name', 'Logger Library')
                root.children().last() + pomConfig

                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.api.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    ext.bintray_user = properties.getProperty('BINTRAY_USER')
    ext.bintray_key = properties.getProperty('BINTRAY_KEY')
}
else {
    ext.bintray_user = System.getenv('BINTRAY_USER')
    ext.bintray_key = System.getenv('BINTRAY_KEY')
}

bintray {
    user = bintray_user
    key = bintray_key
    publications = ['production']
    publish = true
    override = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        licenses = allLicenses
        vcsUrl = gitUrl
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            desc = libraryDescription
            released  = new Date()
        }
    }
}